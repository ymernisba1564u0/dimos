{
  description = "YOUR_DIMOS_PROJECT_DESCRIPTION";

  inputs = {
    nixpkgs.url      = "github:NixOS/nixpkgs/nixos-25.11";
    home-manager.url = "github:nix-community/home-manager/release-25.11";
    home-manager.inputs.nixpkgs.follows = "nixpkgs";
    flake-utils.url  = "github:numtide/flake-utils";
    dimos-flake.url  = "github:jeff-hykin/mystery_test_1/5aeee8b5f6e60239380eb912ac5ec1825edefb4f";
    dimos-flake.inputs.nixpkgs.follows     = "nixpkgs";
    dimos-flake.inputs.flake-utils.follows = "flake-utils";
    xome.url         = "github:jeff-hykin/xome";
    xome.inputs.nixpkgs.follows      = "nixpkgs";
    xome.inputs.flake-utils.follows  = "flake-utils";
    xome.inputs.home-manager.follows = "home-manager";
  };

  outputs = { self, nixpkgs, flake-utils, dimos-flake, xome, ... }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = import nixpkgs { inherit system; };
        dimos = dimos-flake.vars.${system};

        # ------------------------------------------------------------
        # packages
        # ------------------------------------------------------------
        devPackages = dimos.devPackages ++ [
            # add your nix packages here!
            # ex: pkgs.cowsay
        ];

        # ------------------------------------------------------------
        # shell setup (bashrc like)
        # ------------------------------------------------------------
        shellHook = ''
          ${dimos.shellHook}

          # hide the messy nix shell prompt
          if [ "$SHELL" = "bash" ]; then
              export PS1="DimOS dev-shell
> "
          fi

          PROJECT_ROOT=$(git rev-parse --show-toplevel 2>/dev/null || echo "$PWD")
          cd "$PROJECT_ROOT"

          #
          # python & setup
          #
          if [ -f "$PROJECT_ROOT/venv/bin/activate" ]; then
            # if there is a venv, load it
            _nix_python_path="$(realpath "$(which python)")"
            . "$PROJECT_ROOT/venv/bin/activate"
            # check the venv to make sure it wasn't created with a different (non nix) python
            if [ "$_nix_python_path" != "$(realpath "$(which python)")" ]
            then
              echo
              echo
              echo "WARNING:"
              echo "     Your venv was created with something other than the current nix python"
              echo "     This could happen if you made the venv before doing `nix develop`"
              echo "     It could also happen if the nix-python was updated but the venv wasn't"
              echo "     WHAT YOU NEED TO DO:"
              echo "     - If you're about to make/test a PR, delete/rename your venv and run `nix develop` again"
              echo "     - If you're just trying to get the code working, you can continue but you might get bugs FYI"
              echo
              echo
              echo "Got it? (press enter)"; read _
              echo
            fi
          else
            #
            # automate venv setup
            #
            echo "Setting up virtualenv..."
            python3 -m venv venv
            echo "Activating virtualenv..."
            . venv/bin/activate
            # if uv doesnt exist
            if [ -z "$(command -v "uv")" ]; then
                pip install uv
            fi
            # if dimos not available. install it
            if ! python3 -c "try: import dimos
except: exit(1)" 2>/dev/null; then
              if [ "''${DIMOS_ENABLED_FEATURES+dimos_features_are_defined}" = dimos_features_are_defined ]; then
                # no features
                if [ -z "$DIMOS_ENABLED_FEATURES" ]; then
                  # FIXME: change before launch
                  pip install 'dimos @ git+ssh://git@github.com/dimensionalOS/dimos.git'
                else
                  # FIXME: change before launch
                  pip install 'dimos['"$DIMOS_ENABLED_FEATURES"'] @ git+ssh://git@github.com/dimensionalOS/dimos.git'
                fi
              else
                echo
                echo "Don't forget to run: pip install dimos"
              fi
            fi
          fi
        '';

        devShells = {
          # basic shell (blends with your current environment)
          default = pkgs.mkShell {
            buildInputs = devPackages;
            shellHook = shellHook;
          };
          # strict shell (creates a fake home, only select exteral commands (e.g. sudo) from your system are available)
          isolated = (xome.simpleMakeHomeFor {
            inherit pkgs;
            pure = true;
            envPassthrough = [ "DIMOS_ENABLED_FEATURES" "NIX_SSL_CERT_FILE" "TERM" "XOME_REAL_HOME" "XOME_REAL_PATH" "XOME_FAKE_HOME" "XOME_REAL_USER" ];
            commandPassthrough = [ "sudo" "nvim" "code" "sysctl" "sw_vers" "git" "vim" "emacs" "openssl" "ssh" "osascript" "otool" "hidutil" "logger" "codesign" ]; # e.g. use external nvim instead of nix's
            # commonly needed for MacOS: [ "osascript" "otool" "hidutil" "logger" "codesign" ]
            homeSubpathPassthrough = [ "cache/nix/" ]; # share nix cache between projects
            homeModule = {
              # for home-manager examples, see:
              # https://deepwiki.com/nix-community/home-manager/5-configuration-examples
              # all home-manager options:
              # https://nix-community.github.io/home-manager/options.xhtml
              home.homeDirectory = "/tmp/virtual_homes/YOUR_DIMOS_PROJECT_NAME";
              home.stateVersion = "25.11";
              home.packages = devPackages;

              programs = {
                home-manager = {
                  enable = true;
                };
                zsh = {
                  enable = true;
                  enableCompletion = true;
                  autosuggestion.enable = true;
                  syntaxHighlighting.enable = true;
                  shellAliases.ll = "ls -la";
                  history.size = 100000;
                  # this is kinda like .zshrc
                  initContent = ''
                    # most people expect comments in their shell to to work
                    setopt interactivecomments
                    # fix emoji prompt offset issues (this shouldn't lock people into English b/c LANG can be non-english)
                    export LC_CTYPE=en_US.UTF-8
                    ${shellHook}
                  '';
                };
                starship = {
                  enable = true;
                  enableZshIntegration = true;
                  settings = {
                    character = {
                      success_symbol = "[∫](bold green)";
                      error_symbol = "[∫](bold red)";
                    };
                  };
                };
              };
            };
          }).default;
        };
      in {
        ## Local dev shell
        devShells = devShells;
      });
}
